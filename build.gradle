plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
    mavenCentral()
}

project.ext.v_lwjgl = '3.2.1'
project.ext.OS = System.getProperty('os.name').toLowerCase((Locale.ENGLISH))
if (OS.contains('win')) project.ext.OS = 'windows'
if (OS.contains('nux')) project.ext.OS = 'linux'
if (OS.contains('mac')) {
    project.ext.OS = 'macos'
    applicationDefaultJvmArgs = ['-XstartOnFirstThread']
}

dependencies {
    implementation "org.lwjgl:lwjgl:$v_lwjgl"
    runtimeOnly "org.lwjgl:lwjgl:$v_lwjgl:natives-$OS"

    implementation "org.lwjgl:lwjgl-glfw:$v_lwjgl"
    runtimeOnly "org.lwjgl:lwjgl-glfw:$v_lwjgl:natives-$OS"

    implementation "org.lwjgl:lwjgl-opengl:$v_lwjgl"
    runtimeOnly "org.lwjgl:lwjgl-opengl:$v_lwjgl:natives-$OS"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java.srcDirs = ['src']
        resources.srcDirs = ['res']
    }

    test {
        java.srcDirs = ['test']
        resources.srcDirs = ['test/res']
    }
}

mainClassName = 'Main'
